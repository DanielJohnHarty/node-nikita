
# `docker_build(options, callback)`

Build docker image from Dockerfile.
The user can hoose whether the build is local or on the remote.
It copies/downloads the Dockerfile to /var/ryba/docker_timestamp/
on the remote/local machine.
Then run docker build with the option.
Options are the same than docker build command with mecano's one.
Be aware than you can not use ADD with content option because docker build
from STDIN does not support a context.

## Options

*   `image` (string)
    Name of the image. MANDATORY
*   `machine` (string)
    Name of the docker-machine. MANDATORY if using docker-machine
*   `dockerfile`
    Dockerfile path
*   `content` (string | [string]) (Note supported for now )
    Use this text to build the image.
*   `quiet` (boolean)
    Suppress the verbose output generated by the containers. Default to false
*   `rm` (boolean)
    Remove intermediate containers after build. Default to false
*   `force_rm` (boolean)
    Always remove intermediate containers during build. Default to false
*   `no_cache` (boolean)
    Do not use cache when building the image. Default to false
*   `code`   (int|array)
    Expected code(s) returned by the command, int or array of int, default to 0.
*   `code_skipped`
    Expected code(s) returned by the command if it has no effect, executed will
    not be incremented, int or array of int.
*   `cwd` (string)
    change the working directory for the build.
*   `log`
    Function called with a log related messages.
*   `ssh` (object|ssh2)
    Run the action on a remote server using SSH, an ssh2 instance or an
    configuration object used to initialize the SSH connection.
*   `stdout` (stream.Writable)
    Writable EventEmitter in which the standard output of executed commands will
    be piped.
*   `stderr` (stream.Writable)
    Writable EventEmitter in which the standard error output of executed command
    will be piped.

## Callback parameters

*   `err`
    Error object if any.
*   `executed`
    if command was executed
*   `stdout`
    Stdout value(s) unless `stdout` option is provided.
*   `stderr`
    Stderr value(s) unless `stderr` option is provided.

## Example

1- builds an image from dockerfile without any resourcess

```javascript
mecano.docker_build({
  image: 'ryba/targe-build'
  source: '/home/ryba/Dockerfile'
}, function(err, is_true, stdout, stderr){
  if(err){
    console.log(err.message);
  }else if(is_true){
    console.log('OK!');
  }else{
    console.log('Ooops!');
  }
})
```

2- builds an image from dockerfile with external resources

In this case mecano download all the external files into a resources directory in the same location
than the Dockerfile.
The Dockerfile content:   "
                            FROM centos6
                            ADD resources/package.tar.gz /tmp/
                            ADD resources/configuration.sh /tmp/
                          "
Build directory tree :
                          ├── Dockerfile
                          ├── resources
                          │   ├── package.tar.gz
                          │   ├── configuration.sh

```javascript
mecano.docker_build({
  ssh: ssh
  image: 'ryba/target-build'
  source: '/home/ryba/Dockerfile'
  resources: ['http://url.com/package.tar.gz/','/home/configuration.sh']
}, function(err, is_built, stdout, stderr){
  if(err){
    console.log(err.message);
  }else if(is_built){
    console.log('OK!');
  }else{
    console.log('Ooops!');
  }
})
```

3- builds an image from stdin

```javascript
mecano.docker_build({
  ssh: ssh
  image: 'ryba/target-build'
  content: "FROM ubuntu\nRUN echo 'helloworld'"
}, function(err, is_built, stdout, stderr){
  if(err){
    console.log(err.message);
  }else if(is_built){
    console.log('OK!');
  }else{
    console.log('Ooops!');
  }
})
``

## Source Code

    module.exports = (options, callback) ->
      # Validate parameters and madatory conditions
      return callback  Error 'Missing image parameter' unless options.image?
      return callback  Error 'Can not build from Dockerfile and content' if options.content? and options.dockerfile?
      docker.get_provider options, (err,  provider) =>
        return callback err if err
        options.provider = provider
        cmd = docker.prepare_cmd provider, options.machine
        return callback cmd if util.isError cmd
        # custom command for content option
        cmd += 'docker build'
        # not mandatory options
        for opt in ['force_rm', 'quiet', 'no_cache']
          cmd += " --#{opt.replace '_', '-'}" if options[opt]
        cmd += ' --rm=false' if options.rm? and not options.rm
        cmd += " -t \"#{options.image}\""
        # custom command for content option
        if options.dockerfile?
          options.log? "Building from Dockerfile: '#{options.dockerfile}' [INFO]"
          cmd += " -f #{options.dockerfile} #{path.dirname options.dockerfile}"
        else if options.content?
          options.log? "Building from text: Docker won't have a context. ADD/COPY not working [WARN]"
          cmd += " - <<DOCKERFILE\n#{options.content}\nDOCKERFILE" if options.content?
        else
          options.log? "Building from CWD [INFO]"
          cmd += ' .'
        # Construct other exec parameter
        exec_opts =
          cmd: cmd
        for k in ['ssh','log', 'stdout','stderr','cwd','code','code_skipped']
          exec_opts[k] = options[k] if options[k]?
        @execute exec_opts, (err, executed, stdout, stderr) -> callback err, executed, stdout, stderr
        # .execute exec_opts
        # .then (err, executed) ->
        #   return callback err if err
        #   callback null, executed

## Modules Dependencies

    docker = require './commons'
    path = require 'path'
    util = require 'util'
