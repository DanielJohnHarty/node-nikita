
# `docker_build(options, callback)`

Build docker repository from Dockerfile, from content or from current working
directory (exclusive options).
The user can choose whether the build is local or on the remote.
Options are the same than docker build command with mecano's one.
Be aware than you can not use ADD with content option because docker build
from STDIN does not support a context.
By default docker always run the build and overwrite existing repositories.
Status unmodified if the repository is identical to a previous one

## Options

*   `tag` (string)
    Name of the repository. MANDATORY
*   `machine` (string)
    Name of the docker-machine. MANDATORY if using docker-machine
*   `file`
    Dockerfile path
*   `content` (string | [string])
    Use this text to build the repository.
*   `quiet` (boolean)
    Suppress the verbose output generated by the containers. Default to false
*   `rm` (boolean)
    Remove intermediate containers after build. Default to false
*   `force_rm` (boolean)
    Always remove intermediate containers during build. Default to false
*   `no_cache` (boolean)
    Do not use cache when building the repository. Default to false
*   `code`   (int|array)
    Expected code(s) returned by the command, int or array of int, default to 0.
*   `code_skipped`
    Expected code(s) returned by the command if it has no effect, executed will
    not be incremented, int or array of int.
*   `cwd` (string)
    change the working directory for the build.
*   `log`
    Function called with a log related messages.
*   `ssh` (object|ssh2)
    Run the action on a remote server using SSH, an ssh2 instance or an
    configuration object used to initialize the SSH connection.
*   `stdout` (stream.Writable)
    Writable EventEmitter in which the standard output of executed commands will
    be piped.
*   `stderr` (stream.Writable)
    Writable EventEmitter in which the standard error output of executed command
    will be piped.

## Callback parameters

*   `err`
    Error object if any.
*   `executed`
    if command was executed
*   `stdout`
    Stdout value(s) unless `stdout` option is provided.
*   `stderr`
    Stderr value(s) unless `stderr` option is provided.

## Example

1- builds a repository from dockerfile without any resourcess

```javascript
mecano.docker_build({
  tag: 'ryba/targe-build'
  source: '/home/ryba/Dockerfile'
}, function(err, is_true, stdout, stderr){
  if(err){
    console.log(err.message);
  }else if(is_true){
    console.log('OK!');
  }else{
    console.log('Ooops!');
  }
})
```

2- builds an repository from dockerfile with external resources

In this case mecano download all the external files into a resources directory in the same location
than the Dockerfile.
The Dockerfile content:   "
                            FROM centos6
                            ADD resources/package.tar.gz /tmp/
                            ADD resources/configuration.sh /tmp/
                          "
Build directory tree :
                          ├── Dockerfile
                          ├── resources
                          │   ├── package.tar.gz
                          │   ├── configuration.sh

```javascript
mecano.docker_build({
  ssh: ssh
  tag: 'ryba/target-build'
  source: '/home/ryba/Dockerfile'
  resources: ['http://url.com/package.tar.gz/','/home/configuration.sh']
}, function(err, is_built, stdout, stderr){
  if(err){
    console.log(err.message);
  }else if(is_built){
    console.log('OK!');
  }else{
    console.log('Ooops!');
  }
})
```

3- builds an repository from stdin

```javascript
mecano.docker_build({
  ssh: ssh
  tag: 'ryba/target-build'
  content: "FROM ubuntu\nRUN echo 'helloworld'"
}, function(err, is_built, stdout, stderr){
  if(err){
    console.log(err.message);
  }else if(is_built){
    console.log('OK!');
  }else{
    console.log('Ooops!');
  }
})
``

## Source Code

    module.exports = (options, callback) ->
      # Validate parameters and madatory conditions
      return callback Error 'Missing tag parameter' unless options.tag?
      return callback Error 'Can not build from Dockerfile and content' if options.content? and options.path?
      cmd = ' build '
      # not mandatory options
      for opt in ['force_rm', 'quiet', 'no_cache']
        cmd += " --#{opt.replace '_', '-'}" if options[opt]
      cmd += ' --rm=false' if options.rm? and not options.rm
      cmd += " -t \"#{options.tag}\""
      # custom command for content option
      if options.path?
        options.log message: "Building from Dockerfile: \"#{options.path}\"", level: 'INFO', module: 'mecano/docker/build'
        cmd += " -f #{options.path} #{path.dirname options.path}"
      else if options.content?
        options.log message: "Building from text: Docker won't have a context. ADD/COPY not working", level: 'WARN', module: 'mecano/docker/build'
        cmd += " - <<DOCKERFILE\n#{options.content}\nDOCKERFILE" if options.content?
      else
        options.log message: "Building from CWD", level: 'INFO', module: 'mecano/docker/build'
        cmd += ' .'
      # status unmodified if final tag already exists
      dockerfile_cmds = ['CMD','LABEL','EXPOSE','ENV','ADD','COPY','ENTRYPOINT',
       'VOLUME','USER','WORKDIR','ARG','ONBUILD','RUN','STOPSIGNAL','MAINTAINER']
      number_of_step = 0
      @write
         destination: '/tmp/ryba/build/Dockerfile'
         content: options.content
         if: options.content
      , (err) ->
        return callback err if err
        dockerfile_path = '/tmp/ryba/build/Dockerfile' if options.content?
        dockerfile_path = options.path if options.path?
        dockerfile_path = "#{path.resolve options.cwd, 'Dockerfile'}" if (not options.content? and not options.path?)
        options.log message: "Writing Dockerfile to :#{dockerfile_path}", level: 'INFO', module: 'mecano/src/build' if options.content?
        options.log message: "Reading Dockerfile from :#{dockerfile_path}", level: 'INFO', module: 'mecano/src/build' if options.cwd? or options.path?
        fs.readFile options.ssh, dockerfile_path, (err, result) =>
          if err
            if err.code == 'ENOENT'
              options.log message: "Dockerfile :#{dockerfile_path} does not exist", level: 'ERROR', module: 'mecano/src/docker/build'
            return callback(err, false)
          else
            lines = string.lines(result.toString())
            for l in lines
              line =  l.replace(/ /g,'')
              for dockerfile_cmd in dockerfile_cmds
                if (line.indexOf(dockerfile_cmd) is 0)
                  number_of_step = number_of_step + 1
            @remove
              destination: '/tmp/ryba/build/Dockerfile'
              if: options.content
            , (err) ->
              return callback err if err
              options.log message: "Building docker repository:#{options.tag}", level: 'INFO', module: 'mecano/src/docker/build'
              docker.exec cmd, options, null, (err, executed, stdout, stderr) =>
                return callback err, executed, stdout, stderr, container_id_hash if err
                container_id_hash = null
                if executed
                  lines = string.lines(stdout)
                  number_of_cache = 0
                  for k,  line of lines
                    if (line.indexOf('Using cache') isnt  -1 )
                      number_of_cache = number_of_cache + 1
                    if (line.indexOf('Successfully built') isnt  -1 )
                      container_id_hash = line.split(' ').pop().toString()
                executed = null if number_of_step == number_of_cache
                options.log message: "new repository is identical to previous #{options.tag}", level: 'INFO', module: 'mecano/src/docker/build' if executed == null
                options.log message: "new repository hash  #{container_id_hash}", level: 'INFO', module: 'mecano/src/docker/build' if executed
                callback err, executed, stdout, stderr, container_id_hash

## Modules Dependencies


    docker = require '../misc/docker'
    string = require '../misc/string'
    path = require 'path'
    util = require 'util'
    fs = require 'ssh2-fs'
