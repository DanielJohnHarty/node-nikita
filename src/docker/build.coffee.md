
# `docker_build(options, callback)`

Build docker repository from Dockerfile, from content or from current working
directory (exclusive options).
The user can choose whether the build is local or on the remote.
Options are the same than docker build command with mecano's one.
Be aware than you can not use ADD with content option because docker build
from STDIN does not support a context.
By default docker always run the build and overwrite existing repositories.
Status unmodified if the repository is identical to a previous one

## Options

*   `tag` (string)
    Name of the repository. MANDATORY
*   `machine` (string)
    Name of the docker-machine. MANDATORY if using docker-machine
*   `file`
    Dockerfile path
*   `content` (string | [string])
    Use this text to build the repository.
*   `quiet` (boolean)
    Suppress the verbose output generated by the containers. Default to false
*   `rm` (boolean)
    Remove intermediate containers after build. Default to false
*   `force_rm` (boolean)
    Always remove intermediate containers during build. Default to false
*   `no_cache` (boolean)
    Do not use cache when building the repository. Default to false
*   `code`   (int|array)
    Expected code(s) returned by the command, int or array of int, default to 0.
*   `code_skipped`
    Expected code(s) returned by the command if it has no effect, executed will
    not be incremented, int or array of int.
*   `cwd` (string)
    change the working directory for the build.
*   `log`
    Function called with a log related messages.
*   `ssh` (object|ssh2)
    Run the action on a remote server using SSH, an ssh2 instance or an
    configuration object used to initialize the SSH connection.
*   `stdout` (stream.Writable)
    Writable EventEmitter in which the standard output of executed commands will
    be piped.
*   `stderr` (stream.Writable)
    Writable EventEmitter in which the standard error output of executed command
    will be piped.

## Callback parameters

*   `err`
    Error object if any.
*   `status`
    True if image was successfully built.
*   `checksum`
    Image cheksum if built.
*   `stdout`
    Stdout value(s) unless `stdout` option is provided.
*   `stderr`
    Stderr value(s) unless `stderr` option is provided.

## Example

1- builds a repository from dockerfile without any resourcess

```javascript
mecano.docker_build({
  tag: 'ryba/targe-build'
  source: '/home/ryba/Dockerfile'
}, function(err, is_true, stdout, stderr){
  if(err){
    console.log(err.message);
  }else if(is_true){
    console.log('OK!');
  }else{
    console.log('Ooops!');
  }
})
```

2- builds an repository from dockerfile with external resources

In this case mecano download all the external files into a resources directory in the same location
than the Dockerfile.
The Dockerfile content:   "
                            FROM centos6
                            ADD resources/package.tar.gz /tmp/
                            ADD resources/configuration.sh /tmp/
                          "
Build directory tree :
                          ├── Dockerfile
                          ├── resources
                          │   ├── package.tar.gz
                          │   ├── configuration.sh

```javascript
mecano.docker_build({
  ssh: ssh
  tag: 'ryba/target-build'
  source: '/home/ryba/Dockerfile'
  resources: ['http://url.com/package.tar.gz/','/home/configuration.sh']
}, function(err, is_built, stdout, stderr){
  if(err){
    console.log(err.message);
  }else if(is_built){
    console.log('OK!');
  }else{
    console.log('Ooops!');
  }
})
```

3- builds an repository from stdin

```javascript
mecano.docker_build({
  ssh: ssh
  tag: 'ryba/target-build'
  content: "FROM ubuntu\nRUN echo 'helloworld'"
}, function(err, is_built, stdout, stderr){})
``

## Source Code

    module.exports = (options, callback) ->
      # Validate parameters and madatory conditions
      return callback Error 'Required option "tag"' unless options.tag?
      return callback Error 'Can not build from Dockerfile and content' if options.content? and options.file?
      cmd = ' build '
      number_of_step = 0
      userargs = []
      # status unmodified if final tag already exists
      dockerfile_cmds = ['CMD','LABEL','EXPOSE','ENV','ADD','COPY','ENTRYPOINT',
       'VOLUME','USER','WORKDIR','ARG','ONBUILD','RUN','STOPSIGNAL','MAINTAINER']
      source = null
      if options.file
        source = options.file
      else if options.cwd
        source = "#{options.cwd}/Dockerfile"
      options.cwd ?= path.dirname options.file if options.file
      # Apply search and replace to content
      # options.write ?= []
      # if options.from? or options.to? or options.match? or options.replace? or options.before?
      #   options.write.push
      #     from: options.from
      #     to: options.to
      #     match: options.match
      #     replace: options.replace
      #     append: options.append
      #     before: options.before
      #   options.append = false
      @write
        if: options.content
        content: options.content
        source: source
        destination: (content) ->
          # console.log content
          options.content = content
        from: options.from
        to: options.to
        match: options.match
        replace: options.replace
        append: options.append
        before: options.before
        write: options.write
      @call -> # Build cmd
        for opt in ['force_rm', 'quiet', 'no_cache']
          cmd += " --#{opt.replace '_', '-'}" if options[opt]
        cmd += " --rm=#{if options.rm then 'true' else 'false'}"
        cmd += " -t \"#{options.tag}\""
        # custom command for content option
        options.file ?= path.resolve options.cwd, 'Dockerfile' if options.cwd
        if options.content?
          options.log message: "Building from text: Docker won't have a context. ADD/COPY not working", level: 'WARN', module: 'mecano/docker/build'
          cmd += " - <<DOCKERFILE\n#{options.content}\nDOCKERFILE" if options.content?
        else if options.file?
          options.log message: "Building from Dockerfile: \"#{options.file}\"", level: 'INFO', module: 'mecano/docker/build'
          cmd += " -f #{options.file} #{options.cwd}"
        else
          options.log message: "Building from CWD", level: 'INFO', module: 'mecano/docker/build'
          cmd += ' .'
      @call # Read Dockerfile if necessary to count steps
        unless: options.content
        handler: (_, callback) ->
          options.log message: "Reading Dockerfile from : #{options.file}", level: 'INFO', module: 'mecano/lib/build'
          fs.readFile options.ssh, options.file, 'utf8', (err, content) ->
            return callback err if err
            options.content = content
            callback()
      @call -> # Count steps
        for line in string.lines options.content
          number_of_step++ if /^(.*?)\s/.exec(line)?[1] in dockerfile_cmds
      @call ->
        @execute
          cmd: docker.wrap options, cmd
          cwd: options.cwd
        , (err, executed, stdout, stderr) ->
          throw err if err
          container_id_hash = null
          lines = string.lines stderr
          lines = string.lines stdout
          number_of_cache = 0
          for k,  line of lines
            if (line.indexOf('Using cache') isnt  -1 )
              number_of_cache = number_of_cache + 1
            if (line.indexOf('Successfully built') isnt  -1 )
              container_id_hash = line.split(' ').pop().toString()
          userargs = [number_of_step isnt number_of_cache, container_id_hash, stdout, stderr]
      .then (err, status) ->
        return callback err if err
        options.log if userargs[0]
        then message: "New image id #{userargs[1]}", level: 'INFO', module: 'mecano/lib/docker/build' 
        else message: "Identical image id #{userargs[1]}", level: 'INFO', module: 'mecano/lib/docker/build'
        callback null, userargs...

## Modules Dependencies

    docker = require '../misc/docker'
    string = require '../misc/string'
    path = require 'path'
    util = require 'util'
    fs = require 'ssh2-fs'
