
# `docker_build(options, callback)`

Build docker image from Dockerfile.
The user can hoose whether the build is local or on the remote.
It copies/downloads the Dockerfile to /var/ryba/docker_timestamp/ on the remote/local machine.
Then run docker build with the option.
Options are the same than docker build command with mecano's one.
To use local mode, set ssh to null
Be aware than you can not use ADD in write option because docker build from STDIN does support
the context.

## Options

*   `name` (string)
    Name of the image. MANDATORY
*   `machine` (string)
    Name of the docker-machine. MANDATORY if docker-machine installed
*   `source`
    Dockerfile absolute path http or local. MANDATORY if the write option is specified
*   `dockerfile` (string)
    Name of the dockerfile name.Default to `Dockerfile`
*   `write' (string | [string]) (Note supported for now )
    Use this write content to build the image.
*   `quiet` (boolean)
    Suppress the verbose output generated by the containers. Default to false
*   `resources` ([string])
    Array of files path to add in the Dockerfile directory, for case the docker file needs external resources
*   `rm_intermediate` (boolean)
    Remove intermediate containers during build. Default to false
*   `cache` (boolean)
    Use cache when building the image.Default to true
*   `rm_final` (boolean)
    Remove intermediate containers after build. Default to false
*   `pull` (boolean)
    Always try to pull newest image. Default to true
*   `code`   (int|array)
    Expected code(s) returned by the command, int or array of int, default to 0.
*   `code_skipped`
    Expected code(s) returned by the command if it has no effect, executed will
    not be incremented, int or array of int.
*   `cwd` (string)
    change the working directory for the build.
    Recommanded if the image is built from write and needs resources relative to it
*   `log`
    Function called with a log related messages.
*   `ssh` (object|ssh2)
    Run the action on a remote server using SSH, an ssh2 instance or an
    configuration object used to initialize the SSH connection.
*   `stdout` (stream.Writable)
    Writable EventEmitter in which the standard output of executed commands will
    be piped.
*   `stderr` (stream.Writable)
    Writable EventEmitter in which the standard error output of executed command
    will be piped.

## Callback parameters

*   `err`
    Error object if any.
*   `executed`
    if command was executed
*   `stdout`
    Stdout value(s) unless `stdout` option is provided.
*   `stderr`
    Stderr value(s) unless `stderr` option is provided.

## Example

1- builds an image from dockerfile without any resourcess

```javascript
mecano.docker_build({
  name: 'ryba/targe-build'
  source: '/home/ryba/Dockerfile'
}, function(err, is_true, stdout, stderr){
  if(err){
    console.log(err.message);
  }else if(is_true){
    console.log('OK!');
  }else{
    console.log('Ooops!');
  }
})
```

2- builds an image from dockerfile with external resources

In this case mecano download all the external files into a resources directory in the same location
than the Dockerfile.
The Dockerfile content:   "
                            FROM centos6
                            ADD resources/package.tar.gz /tmp/
                            ADD resources/configuration.sh /tmp/
                          " 
Build directory tree :
                          ├── Dockerfile
                          ├── resources
                          │   ├── package.tar.gz
                          │   ├── configuration.sh

```javascript
mecano.docker_build({
  ssh: ssh
  name: 'ryba/target-build'
  source: '/home/ryba/Dockerfile'
  resources: ['http://url.com/package.tar.gz/','/home/configuration.sh']
}, function(err, is_built, stdout, stderr){
  if(err){
    console.log(err.message);
  }else if(is_built){
    console.log('OK!');
  }else{
    console.log('Ooops!');
  }
})
```

3- builds an image from stdin 

```javascript
mecano.docker_build({
  ssh: ssh
  name: 'ryba/target-build'
  write: "FROM ubuntu\nRUN echo 'helloworld'"
}, function(err, is_built, stdout, stderr){
  if(err){
    console.log(err.message);
  }else if(is_built){
    console.log('OK!');
  }else{
    console.log('Ooops!');
  }
})
``

## Source Code

    module.exports = (options, callback) ->
      # Validate parameters and madatory conditions
      return callback  Error 'Missing build name' unless options.name?
      return callback  Error 'source and write not specified' unless options.write? or options.source?
      return callback  Error 'can not build from source and write' if options.write? and options.source?
      return callback  Error 'docker does not support adding resource from STDIN docker build ' if options.write? and options.resources?
      
      #options.dockerfile ?= "Dockerfile"
      options.rm_intermediate ?= false
      options.rm_final = false
      options.pull ?= true
      options.quiet ?= false
      options.cache = !options.cache ?= false
      options.destination = options.cwd ?= "/tmp/ryba/build_#{Date.now()}"
      options.shy = true
      do_download_resources = =>
        #download the resources needed by the dockerfile
        if options.resources?
          dl = []
          for path in options.resources 
            dl.push
              source: path
              destination: "#{options.destination}/resources/}"
              cache: true
              ssh: options.ssh
              force: true
          @child()
          .download dl
          .then (err,executed) ->
            return callback err if err
            #do_download_docker_file()
        else
          do_download_docker_file()
      do_download_docker_file = =>
        @child()
        .download
          ssh: options.ssh
          source: options.source
          cache: true
          force: true
          destination: "#{options.destination}/Dockerfile"
        , (err,executed) ->
          return callback err if err
          do_provider()
      do_provider = =>
        docker.check_docker_daemon_provider(options, do_construct_command)
      do_construct_command = (err,  provider) =>
        return callback err if err
        options.provider = provider
        switch  provider
          when 'docker-machine','boot2docker' then cmd += docker.get_env_expr provider,  options.machine
        # custom command for write option
        cmd += " echo #{options.write}  | " if options.write?
        cmd += 'docker build'
        # mandatory name option
        cmd += " --tag=\"#{options.name}\""
        # not mandatory options
        for opt, flag of { rm_intermediate: '--force-rm',rm_final: '--rm', quiet: '--quiet', cache: '--no-cache'}
          cmd += " #{flag}=#{options[opt]}" if options[opt]?
        # custom command for write option
        cmd += " #{options.destination}" if options.source?
        cmd += " - " if options.write?
        # Construct other exec parameter
        exec_opts =
          cmd: cmd
        for k in ['ssh','log', 'stdout','stderr','cwd','code','code_skipped']
          exec_opts[k] = options[k] if options[k]?
        @execute exec_opts, (err, executed, stdout, stderr) -> callback err, executed, stdout, stderr
        # .execute exec_opts 
        # .then (err, executed) ->
        #   return callback err if err
        #   callback null, executed
      cmd = ''
      if options.source? then do_download_resources() else do_provider()


    path = require 'path'
    docker = require './misc/docker'
